//file:noinspection GroovyAssignabilityCheck

plugins {
    id("java")
    id("checkstyle")
    id("com.github.spotbugs").version("5.0.3")
    id("org.cadixdev.licenser").version("0.6.1")
}

setGroup("net.elytrium")
setVersion("1.0.3-SNAPSHOT")

compileJava {
    getOptions().setEncoding("UTF-8")
}

java {
    setSourceCompatibility(JavaVersion.VERSION_11)
    setTargetCompatibility(JavaVersion.VERSION_11)
}

repositories {
    mavenCentral()

    maven {
        setName("velocitypowered-repo")
        setUrl("https://nexus.velocitypowered.com/repository/maven-public/")
    }
    maven {
        setName("elytrium-repo")
        setUrl("https://maven.elytrium.net/repo/")
    }
}

dependencies {
    compileOnly("net.elytrium:limboapi-api:1.0.3-SNAPSHOT")

    compileOnly("com.velocitypowered:velocity-api:3.1.0")
    annotationProcessor("com.velocitypowered:velocity-api:3.1.0")
    compileOnly("com.velocitypowered:velocity-proxy:3.1.0") // From Elytrium Repo

    // Needs for some velocity methods
    compileOnly("io.netty:netty-codec:4.1.72.Final")

    compileOnly("com.github.spotbugs:spotbugs-annotations:4.5.2")
}

license {
    setHeader(file("HEADER.txt"))
}

checkstyle {
    setToolVersion("9.2")
    setConfigFile(file("${this.getRootDir()}/config/checkstyle/checkstyle.xml"))
    setConfigProperties("configDirectory": "${this.getRootDir()}/config/checkstyle")

    // The build should immediately fail if we have errors.
    setMaxErrors(0)
    setMaxWarnings(0)
}

spotbugsMain {
    setExcludeFilter(file("${this.getRootDir()}/config/spotbugs/suppressions.xml"))

    reports {
        html {
            getRequired().set(true)
            getOutputLocation().set(file("${this.getBuildDir()}/reports/spotbugs/main/spotbugs.html"))
            setStylesheet("fancy-hist.xsl")
        }
    }
}

sourceSets.main.getJava().srcDir(getTasks().register("generateTemplates", Copy) { task ->
    task.getInputs().properties("version": getVersion())

    task.from(file("src/main/templates"))
        .into(getLayout().getBuildDirectory().dir("generated/sources/templates"))
        .expand("version": getVersion())
}.map {
    it.getOutputs()
})
